#+title: Just a Literate Emacs Configuration
#+author: Icy-Thought
#+language: en
#+STARTUP: showeverything

#+NAME: irkalla-emacs.png
#+CAPTION: Showcasing my very first standalone Emacs configuration.
[[../.github/assets/png/irkalla.png]]

* Table of Contents :TOC:
- [[#early-initialization-of-irkalla-emacs][Early Initialization of Irkalla Emacs]]
  - [[#performance-configurations][Performance Configurations]]
    - [[#initialize-use-package-at-a-later-stage][Initialize ~use-package~ at a Later Stage]]
    - [[#quicker-launches-in-fundamental-mode][Quicker Launches in Fundamental-mode]]
    - [[#dont-compact-font-cache-on-gc][Don't Compact Font-cache on GC]]
    - [[#increase-processing-power][Increase Processing Power]]
    - [[#defining-user-directories][Defining User Directories]]
    - [[#native-compilation][Native Compilation]]
  - [[#reducing-noise-on-start][Reducing Noise on Start]]
    - [[#start-up-messages][Start-up Messages]]
    - [[#prevent-for-information-messages][Prevent "For information..." Messages]]
    - [[#loading-x-files-echos][Loading =X= Files Echo's]]
  - [[#remaining-modifications][Remaining Modifications]]
    - [[#encoding-system---utf-8][Encoding System -> UTF-8]]
    - [[#load-recently-compiled-el-files][Load Recently Compiled =.el= Files]]
    - [[#prevent-frame-resize-on-launch][Prevent Frame Resize on Launch]]
  - [[#early-irkalla-decorations][Early Irkalla Decorations]]
  - [[#closing-early-init][Closing Early-Init!]]
- [[#embarking-on-the-irkalla-journey-initialization][Embarking on the Irkalla Journey: Initialization.]]
  - [[#the-beginning-of-irkalla-emacs][The Beginning of Irkalla Emacs]]
    - [[#elpaca-package-manager][Elpaca: Package Manager]]
    - [[#modifying-irkallas-many-behaviours][Modifying Irkalla's Many Behaviours]]
    - [[#enhancing-the-aesthetics-and-functionality][Enhancing the Aesthetics and Functionality]]
    - [[#unraveling-a-multitude-of-powerful-options][Unraveling a Multitude of Powerful Options]]
  - [[#expanding-irkallas-abilities][Expanding Irkalla's Abilities]]
    - [[#supercharging-with-essentials][Supercharging with Essentials]]
    - [[#applying-vibrant-colors-to-irkalla][Applying Vibrant Colors To Irkalla]]
    - [[#unleashing-the-power-of-ui-extensions][Unleashing the Power of UI Extensions]]
    - [[#building-a-versatile-toolbox][Building a Versatile Toolbox]]
    - [[#supercharging-irkallas-mobility][Supercharging Irkalla's Mobility]]
    - [[#tailoring-our-editing-experience][Tailoring our Editing Experience]]
    - [[#step-1-a-more-efficient-completion-system][Step-1: A More Efficient Completion System]]
    - [[#step-2-lsp-diagnostics--syntax-highlighting][Step-2: LSP, Diagnostics & Syntax Highlighting!]]
    - [[#step-3-language-specific-adjustments][Step-3: Language-specific Adjustments]]
  - [[#concluding-our-journey-with-irkalla-emacs][Concluding Our Journey with Irkalla Emacs]]

* Early Initialization of Irkalla Emacs
:PROPERTIES:
:header-args+: :tangle "~/.config/emacs/early-init.el" :noweb yes
:END:

Complying with the [[https://gnu.org/software/emacs/manual/html_node/elisp/Library-Headers.html][Emacs convention]] with the intention of making it a tad easier for people to determine who the hell
wrote this madness.. (ðŸ˜…)

#+begin_src emacs-lisp
;;; early-init.el --- Icy-Thoughts's Irkalla Emacs Early Initiliazation -*- lexical-binding: t -*-

;; Copyright (C) 2023-2023 Icy-Thought

;; Author: Icy-Thought <icy-thought@pm.me>
;; Keywords: internal
;; URL: https://icy-thought.github.io/

;;; Commentary:
;; Configurations which ought to be loaded during Irkalla Emacs initliazation process.

;;; Code:
#+end_src

** Performance Configurations

*** Initialize ~use-package~ at a Later Stage

#+begin_src emacs-lisp
(setq package-quickstart nil
      package-enable-at-startup nil)
#+end_src

*** Quicker Launches in Fundamental-mode

#+begin_src emacs-lisp
(setq initial-major-mode 'fundamental-mode)
#+end_src

*** Don't Compact Font-cache on GC

#+begin_src emacs-lisp
(setq inhibit-compacting-font-caches t)
#+end_src

*** Increase Processing Power

#+begin_src emacs-lisp
(when (boundp 'read-process-output-max)
  (setq read-process-output-max (* 24 1024 1024)))
#+end_src

*** Defining User Directories

#+begin_src emacs-lisp
(setq user-emacs-directory (expand-file-name "~/.config/emacs"))
#+end_src

We also want Emacs to become aware of our custom =themes/= directory!

#+begin_src emacs-lisp
(add-to-list 'custom-theme-load-path
             (expand-file-name "lib/themes" user-emacs-directory))
#+end_src

**** Migrating Custom-file -> =/tmp/emacs-custom-*.el=

#+begin_src emacs-lisp
(setq custom-file
      (if (boundp 'server-socket-dir)
          (expand-file-name "custom.el" server-socket-dir)
        (expand-file-name (format "emacs-custom-%s.el" (user-uid)) temporary-file-directory)))

(when (file-exists-p custom-file)
  (load custom-file))
#+end_src

*** Native Compilation

#+begin_src emacs-lisp
(when (featurep 'native-compile)
  ;; :NOTE| Retain native compilation cache files in ~/.cache/emacs directory
  (let ((path (expand-file-name "var/eln-cache/" user-emacs-directory)))
    (setq native-comp-eln-load-path (list path)
          native-compile-target-directory path))

  ;; :NOTE| Reduce native-compilation noise
  (setq native-comp-async-report-warnings-errors nil
        inhibit-automatic-native-compilation t))
#+end_src

** Reducing Noise on Start

*** Start-up Messages

#+begin_src emacs-lisp
(setq inhibit-startup-buffer-menu t
      inhibit-startup-message t
      inhibit-startup-screen t
      inhibit-startup-echo-area-message t)
#+end_src

*** Prevent "For information..." Messages

#+begin_src emacs-lisp
(advice-add #'display-startup-echo-area-message :override #'ignore)
(advice-add #'display-startup-screen :override #'ignore)
#+end_src

*** Loading =X= Files Echo's

#+begin_src emacs-lisp
(unless init-file-debug
  (define-advice load-file (:override (file) silence)
    (load file nil 'nomessage))
  ;; Undo advice until later for proper loading..
  (define-advice startup--load-user-init-file (:before (&rest _) undo-silence)
    (advice-remove #'load-file #'load-file@silence))
  <<disable-emacs-modeline>>)
#+end_src

Disable default Emacs mode-line for a minor launch time boost. (30-50 ms)

#+NAME: disable-emacs-modeline
#+begin_src emacs-lisp :tangle no
(put 'mode-line-format 'initial-value (default-toplevel-value 'mode-line-format))

(setq-default mode-line-format nil)

(dolist (buf (buffer-list))
  (with-current-buffer buf (setq mode-line-format nil)))
#+end_src

** Remaining Modifications

*** Encoding System -> UTF-8

#+begin_src emacs-lisp
(prefer-coding-system 'utf-8)
#+end_src

*** Load Recently Compiled =.el= Files

#+begin_src emacs-lisp
(customize-set-variable 'load-prefer-newer t)
#+end_src

*** Prevent Frame Resize on Launch

#+begin_src emacs-lisp
(setq frame-inhibit-implied-resize t
      frame-resize-pixelwise t)
#+end_src

** Early Irkalla Decorations

#+begin_src emacs-lisp
(setq default-frame-alist
      (append (list
               '(alpha-background . 85)

               ;; :NOTE| Default Irkalla font
               '(font . "VictorMono Nerd Font-13.5:weight=bold:antialias=true")
               '(height . 125)

               ;; :NOTE| Disabling unnecessary bloat..
               '(fullscreen . nil)
               '(menu-bar-lines . 0)
               '(tool-bar-lines . 0)
               '(vertical-scroll-bars . nil))))
#+end_src

#+begin_src emacs-lisp
(set-fontset-font "fontset-default"
                  'arabic (font-spec :family "Scheherazade New;" :size 25))
#+end_src

** Closing Early-Init!

#+begin_src emacs-lisp
;;; early-init.el ends here
#+end_src

* Embarking on the Irkalla Journey: Initialization.
:PROPERTIES:
:header-args+: :tangle "~/.config/emacs/init.el" :noweb yes
:END:

Complying with the [[https://gnu.org/software/emacs/manual/html_node/elisp/Library-Headers.html][Emacs convention]] with the intention of making it a tad easier for people to determine who the hell
wrote this madness.. (ðŸ˜…)

#+begin_src emacs-lisp
;;; init.el --- Icy-Thoughts's Irkalla Emacs configuration -*- lexical-binding: t -*-

;; Copyright (C) 2023-2023 Icy-Thought

;; Author: Icy-Thought <icy-thought@pm.me>
;; Keywords: internal
;; URL: https://icy-thought.github.io/

;;; Commentary:
;; A fully fledged, reproducible Emacs configuration

;;; Code:
#+end_src

** The Beginning of Irkalla Emacs

Boilerplate code that is necessary to prevent garbage and misunderstanding from occurring along the road.

To retain our sanity, let's enable =debug-on-error=

#+begin_src emacs-lisp
(setq debug-on-error init-file-debug)
#+end_src

Allow ~use-package-report~ to inform us about the slow packages we have cursed Irkalla with!

#+begin_src emacs-lisp
(setq use-package-compute-statistics t)
#+end_src

*** Elpaca: Package Manager

**** Bootstrapping According to Official Docs

#+begin_src emacs-lisp
(defvar elpaca-installer-version 0.5)
(defvar elpaca-directory (expand-file-name "elpaca/" user-emacs-directory))
(defvar elpaca-builds-directory (expand-file-name "builds/" elpaca-directory))
(defvar elpaca-repos-directory (expand-file-name "repos/" elpaca-directory))
(defvar elpaca-order '(elpaca :repo "https://github.com/progfolio/elpaca.git"
                              :ref nil
                              :files (:defaults (:exclude "extensions"))
                              :build (:not elpaca--activate-package)))
(let* ((repo  (expand-file-name "elpaca/" elpaca-repos-directory))
       (build (expand-file-name "elpaca/" elpaca-builds-directory))
       (order (cdr elpaca-order))
       (default-directory repo))
  (add-to-list 'load-path (if (file-exists-p build) build repo))
  (unless (file-exists-p repo)
    (make-directory repo t)
    (when (< emacs-major-version 28) (require 'subr-x))
    (condition-case-unless-debug err
        (if-let ((buffer (pop-to-buffer-same-window "*elpaca-bootstrap*"))
                 ((zerop (call-process "git" nil buffer t "clone"
                                       (plist-get order :repo) repo)))
                 ((zerop (call-process "git" nil buffer t "checkout"
                                       (or (plist-get order :ref) "--"))))
                 (emacs (concat invocation-directory invocation-name))
                 ((zerop (call-process emacs nil buffer nil "-Q" "-L" "." "--batch"
                                       "--eval" "(byte-recompile-directory \".\" 0 'force)")))
                 ((require 'elpaca))
                 ((elpaca-generate-autoloads "elpaca" repo)))
            (progn (message "%s" (buffer-string)) (kill-buffer buffer))
          (error "%s" (with-current-buffer buffer (buffer-string))))
      ((error) (warn "%s" err) (delete-directory repo 'recursive))))
  (unless (require 'elpaca-autoloads nil t)
    (require 'elpaca)
    (elpaca-generate-autoloads "elpaca" repo)
    (load "./elpaca-autoloads")))
(add-hook 'after-init-hook #'elpaca-process-queues)
(elpaca `(,@elpaca-order))
#+end_src

**** Basic Configurations

#+begin_src emacs-lisp
(unless (fboundp 'use-package)
  (elpaca use-package (require 'use-package)))

(elpaca elpaca-use-package
  (elpaca-use-package-mode)
  (setq elpaca-use-package-by-default t))
#+end_src

**** Ensure Elpaca ~==~ configured

#+begin_src emacs-lisp
(elpaca-wait)
#+end_src

*** Modifying Irkalla's Many Behaviours

**** Controlling Our Cursed Fonts

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/default-font-family ()
  "Retrieve the font name from default-frame-alist."
  (let ((font-value (cdr (assoc 'font default-frame-alist))))
    (if (stringp font-value)
        (car (split-string font-value "-"))
      font-value)))
#+end_src

#+begin_src emacs-lisp
(use-package face-remap
  :elpaca nil
  :bind (("C-0" . (lambda () (interactive) (text-scale-increase 0.0)))
         ("C-+" . (lambda () (interactive) (text-scale-increase 0.5)))
         ("C--" . (lambda () (interactive) (text-scale-decrease 0.5)))))
#+end_src

***** Applying Font-face across Irkalla

#+begin_src emacs-lisp
(use-package faces
  :elpaca nil
  :custom-face
  (fixed-pitch    ((t (:family (irkalla/default-font-family) :height 125))))
  (variable-pitch ((t (:family (irkalla/default-font-family) :height 135)))))
#+end_src

***** Switching Frame Slants

#+begin_src emacs-lisp
(use-package font-lock
  :elpaca nil
  :defer t
  :custom-face
  (font-lock-builtin-face       ((t (:slant italic))))
  (font-lock-comment-face       ((t (:slant italic))))
  (font-lock-doc-face           ((t (:slant italic))))
  (font-lock-function-name-face ((t (:slant italic :weight bold))))
  (font-lock-keyword-face       ((t (:slant italic))))
  (font-lock-preprocessor-face  ((t (:weight bold))))
  (font-lock-string-face        ((t (:slant italic)))))
#+end_src

**** Profile Irkalla's Startup

#+begin_src emacs-lisp
(use-package esup
  :defer t
  :custom (esup-depth 0))
#+end_src

**** Prevent Littering

#+begin_src emacs-lisp
(use-package no-littering
  :config
  (setq-default
   no-littering-etc-directory (expand-file-name "etc/" user-emacs-directory)   ; :WARN| store configurations
   no-littering-var-directory (expand-file-name "var/" user-emacs-directory))) ; :WARN| store Emacs data
#+end_src

***** Prevent =recentf= From Littering

#+begin_src emacs-lisp
(use-package recentf
  :elpaca nil
  :defer t
  :after no-littering
  :config
  (add-to-list 'recentf-exclude no-littering-etc-directory)
  (add-to-list 'recentf-exclude no-littering-var-directory)

  ;; :NOTE| Ignore Emacs-everywhere file entries
  (add-to-list 'recentf-exclude
               (concat "/tmp/emacs-everywhere")))
#+end_src

**** Limiting File & Backup Age

Storing our ~Auto-Save~ and ~Backup~ files in our ~No-Littering~ directory is trivial!

#+begin_src emacs-lisp
(with-eval-after-load 'no-littering
  (setq-default
   auto-save-file-name-transforms
   `((".*" ,(no-littering-expand-var-file-name "auto-save/") t))

    backup-directory-alist
    `((".*" . ,(no-littering-expand-var-file-name "backups/")))))
#+end_src

Afterwards, proceeding with the backup & auto-save related configurations should not cause us any worry.

#+begin_src emacs-lisp
(use-package emacs
  :elpaca nil
  :custom
  (backup-by-copying t)
  (delete-by-moving-to-trash t)
  (delete-old-versions t)
  (kept-new-versions 10)
  (kept-old-versions 3))
#+end_src

**** Appropriate Garbage Collection

#+begin_src emacs-lisp
(use-package gcmh
  :demand t
  :custom (gcmh-mode 1))
#+end_src

**** Secrets Management Through Agenix

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/read-secret-file (filename)
  "Fetch content of secrets file generated by agenix."
  (with-temp-buffer
    (insert-file-contents (concat "/run/agenix/" filename))
    (string-trim-right (buffer-string))))
#+end_src

**** Prevent Killing Certain Buffers

#+begin_src emacs-lisp
(with-current-buffer "*scratch*"
  (emacs-lock-mode 'kill))

(with-current-buffer "*Messages*"
  (emacs-lock-mode 'kill))
#+end_src

**** Quick-Add Public Projects

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/add-public-projects ()
  "Add directories present in our /public directory to known projects."
  (interactive)
  (let ((base-dir (expand-file-name "~/Workspace/public")))
    (mapc (lambda (dir)
            (when (file-directory-p dir)
              (projectile-add-known-project dir)))
          (directory-files base-dir t "^[^.].*"))
    (message "Directories inside ~/Workspace/public added as known projects.")))
#+END_src

*** Enhancing the Aesthetics and Functionality

Our editing environment should become an environment we would like to spend our time in. Therefore I have chosen to
apply several minor changes with the intentions of increasing the aesthetics of our editing environment by a small
percentage!

1. Set default buffer width to 120 characters.
2. Break long lines into smaller bits and display a marker (â†´) at the end of each line.
3. Break windows into equal portions.
4. Stretch cursor to fit what it hovers above.

#+begin_src emacs-lisp
(use-package emacs
  :elpaca nil
  :custom
  (fill-column 120)
  (truncate-lines t)
  (truncate-string-ellipsis "â†´")
  (window-combination-resize t)
  (x-stretch-cursor t))
#+end_src

**** Transparent Emacs Frame (Toggle)

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/toggle-frame-transparency ()
  "Toggle (on/off) Emacs frame transparency on demand!"
  (interactive)
  (let ((alpha-value
         (if (equal (frame-parameter nil 'alpha-background) 100)
             85 100)))
    (set-frame-parameter nil 'alpha-background alpha-value)
    (add-to-list 'default-frame-alist `(alpha-background . ,alpha-value))))
#+end_src

**** Ligature: Displaying $SYMB Differently

#+begin_src emacs-lisp
(use-package prettify-symbols
  :elpaca nil
  ;; Use-case: lambda -> ð›Œ
  :hook (emacs-lisp-mode . prettify-symbols-mode)
  :custom (prettify-symbols-unprettify-at-point 'right-edge))
#+end_src

#+begin_src emacs-lisp
(use-package ligature
  :defer t
  :hook ((prog-mode text-mode) . ligature-mode)
  :config
  (ligature-set-ligatures 'prog-mode
   '("|||>" "<|||" "<==>" "<!--" "####" "~~>" "***" "||=" "||>"
     ":::" "::=" "=:=" "===" "==>" "=!=" "=>>" "=<<" "=/=" "!=="
     "!!." ">=>" ">>=" ">>>" ">>-" ">->" "->>" "-->" "---" "-<<"
     "<~~" "<~>" "<*>" "<||" "<|>" "<$>" "<==" "<=>" "<=<" "<->"
     "<--" "<-<" "<<=" "<<-" "<<<" "<+>" "</>" "###" "#_(" "..<"
     "..." "+++" "/==" "///" "_|_" "www" "&&" "^=" "~~" "~@" "~="
     "~>" "~-" "**" "*>" "*/" "||" "|}" "|]" "|=" "|>" "|-" "{|"
     "[|" "]#" "::" ":=" ":>" ":<" "$>" "==" "=>" "!=" "!!" ">:"
     ">=" ">>" ">-" "-~" "-|" "->" "--" "-<" "<~" "<*" "<|" "<:"
     "<$" "<=" "<>" "<-" "<<" "<+" "</" "#{" "#[" "#:" "#=" "#!"
     "##" "#(" "#?" "#_" "%%" ".=" ".-" ".." ".?" "+>" "++" "?:"
     "?=" "?." "??" ";;" "/*" "/=" "/>" "//" "__" "~~" "(*" "*)"
     "\\\\" "://")))
#+end_src

**** Display Pop-up Windows, instead of mini-buffer

#+begin_src emacs-lisp
(use-package posframe
  :defer t
  :custom (posframe-mouse-banish '(0 . 5000)))
#+end_src

*** Unraveling a Multitude of Powerful Options

**** Who am I???

#+begin_src emacs-lisp
(setq user-full-name "Icy-Thought"
      user-mail-address "icy-thought@pm.me")
#+end_src

**** Rational Irkalla Emacs Behaviour

#+begin_src emacs-lisp
(use-package emacs
  :elpaca nil
  :custom
  (echo-keystrokes 0.02)
  (enable-recursive-minibuffers t)
  (help-window-select t)
  (inhibit-startup-echo-area-message t)
  (read-file-name-completion-ignore-case t)
  (read-buffer-completion-ignore-case t)
  (ring-bell-function 'ignore)
  (use-dialog-box nil)
  (use-short-answers t))
#+end_src

**** Highlight Cursor-line

#+begin_src emacs-lisp
(use-package hl-line
  :elpaca nil
  :hook (elpaca-after-init . global-hl-line-mode))
#+end_src

**** Auto-revert: Update Inactive Files on-change

#+begin_src emacs-lisp
(use-package auto-revert
  :elpaca nil
  :hook ((prog-mode text-mode) . auto-revert-mode)
  :custom
  (auto-revert-notify t)
  (auto-revert-verbose t))
#+end_src

**** Time: Configuring our Clock

#+begin_src emacs-lisp
(use-package time
  :elpaca nil
  :defer t
  :custom
  (display-time-24hr-format t)
  (display-time-day-and-date t))
#+end_src

**** Number-line: Relative Numbering

#+begin_src emacs-lisp
(use-package display-line-numbers
  :elpaca nil
  :hook ((prog-mode text-mode conf-mode) . display-line-numbers-mode)
  :custom (display-line-numbers-type 'relative))
#+end_src

** Expanding Irkalla's Abilities

*** Supercharging with Essentials

**** General: A More Convenient Method for Binding Keys

#+begin_src emacs-lisp
(use-package general
  :demand t
  :config
  (general-evil-setup)
  (general-override-mode)
  (general-auto-unbind-keys)

  ;; :NOTE| defining several ease-of-use bindings
  (general-create-definer irkalla/space-lead-keydef
    :keymaps 'override
    :states '(emacs insert motion normal visual)
    :prefix "SPC"
    :global-prefix "M-SPC")

  (general-create-definer irkalla/comma-lead-keydef
    :keymaps 'override
    :states '(emacs insert motion normal visual)
    :prefix ","
    :non-normal-prefix "M-,"))

;; :NOTE| Necessary for :general to work with use-package!
(elpaca-wait)
#+end_src

***** (Emacs) Uncategorized General Bindings

#+begin_src emacs-lisp
(use-package emacs
  :elpaca nil
  :general
  (general-def "<escape>" 'keyboard-escape-quit)

  (irkalla/space-lead-keydef
    ;; Buffer-related
    "b"     '(:ignore t        :which-key "Buffer Management")
    "b d"   '(kill-this-buffer :which-key "Exit active buffer")
    "b n"   '(next-buffer      :which-key "Switch -> next buffer")
    "b p"   '(previous-buffer  :which-key "Switch -> prev. buffer")
    "b s"   '(scratch-buffer   :which-key "Switch -> curr. scratch-buf")

    ;; File-related
    "f"     '(:ignore t        :which-key "Find")
    "f RET" '(find-file        :which-key "Files in directory")

    ;; Expression evaluation
    "e"     '(:ignore t        :which-key "Evaluation")
    "e e"   '(eval-expression  :which-key "Evaluate input")
    "e b"   '(eval-buffer      :which-key "Evaluate buffer")

    ;; Project Management
    "p"          '(:ignore t              :which-key "Projects")
    "p r"        '(projectile-replace     :which-key "Search & replace str")

    ;; Manage Emacs session
    "q"     '(:ignore t        :which-key "Manage Emacs sessions")
    "q r"   '(restart-emacs    :which-key "Restart Emacs")
    "q q"   '(kill-emacs       :which-key "Quit Emacs..."))

  (irkalla/space-lead-keydef
    :states 'visual
    "e r"   '(eval-region      :which-key "Eval highlighted")))
#+end_src

***** (Elpaca) Quick-Access To our Package Manager

#+begin_src emacs-lisp
(use-package elpaca
  :elpaca nil
  :general
  (irkalla/comma-lead-keydef
    "p"   '(:ignore t         :which-key "Package Manager")
    "p p" '(elpaca-manager    :which-key "Open Elpaca")
    "p f" '(elpaca-fetch-all  :which-key "Fetch package commits")
    "p u" '(elpaca-update-all :which-key "Update all packages")))
#+end_src

**** Helpful: Contextual & More Convenient Help Menu

#+begin_src emacs-lisp
(use-package helpful
  :general
  (irkalla/space-lead-keydef
    "h"   '(:ignore t        :which-key "Helpful Description")
    "h k" '(helpful-key      :which-key "Key")
    "h f" '(helpful-callable :which-key "Function")
    "h v" '(helpful-variable :which-key "Variable")
    "h C" '(helpful-command  :which-key "Command")
    "h F" '(helpful-function :which-key "Interactive functions"))

  (irkalla/comma-lead-keydef emacs-lisp-mode-map
    "h p" '(helpful-at-point :which-key "Show help for SYMB")))
#+end_src

**** Which-Key: Pop-up That Displays Available Bindings

#+begin_src emacs-lisp
(use-package which-key
  :diminish which-key-mode
  :hook ((elpaca-after-init . which-key-mode)
         (which-key . which-key-setup-minibuffer))
  :custom
  (which-key-allow-evil-operators t)
  (which-key-idle-delay 0.3)
  (which-key-show-remaining-keys t)
  (which-key-separator " â†’ ")
  (which-key-sort-order 'which-key-prefix-then-key-order))
#+end_src

**** Emacs Everywhere: Allow Emacs Universal Access

#+begin_src emacs-lisp
(use-package emacs-everywhere
  :custom (emacs-everywhere-copy-command (list "cb" "copy" "%f")))
#+end_src

*** Applying Vibrant Colors To Irkalla

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/apply-theme (palette)
  "A quicker way to apply our installed themes."
  (if (custom-theme-p palette)
      (enable-theme palette)
    (load-theme palette :no-confirm)))
#+end_src

**** Autothemer: A Convenient Way to Create Emacs Themes!

#+begin_src emacs-lisp
(use-package autothemer)
#+end_src

Enabling our theme has never been more pleasant!

#+begin_src emacs-lisp
(with-eval-after-load 'autothemer
  (irkalla/apply-theme 'kanagawa))
#+end_src

**** Alert: System-integrated Notifier

#+begin_src emacs-lisp
(use-package alert
  :custom (alert-default-style 'libnotify))
#+end_src

**** Matching Parens & Colorful Delimiters

Colorful delimiters are rather useful, especially when dealing with lisp code!

#+begin_src emacs-lisp
(use-package rainbow-delimiters
  :hook (prog-mode . rainbow-delimiters-mode)
  :config)
#+end_src

Also, highlighting the matching =Paren= is very useful when you are lost! (ðŸ˜…)

#+begin_src emacs-lisp
(use-package parens
  :elpaca nil
  :hook (prog-mode . show-paren-mode)
  :custom
  (show-paren-style 'parenthesis)
  (show-paren-when-point-in-periphery nil)
  (show-paren-when-point-inside-paren nil))
#+end_src

*** Unleashing the Power of UI Extensions

**** Centaur Tabs: Tab-bar to Reduce the Burden of Navigation

#+begin_src emacs-lisp
(use-package centaur-tabs
  :hook (elpaca-after-init . centaur-tabs-mode)
  :general (general-nmap centaur-tabs-mode-map
             "C-<prior>" #'centaur-tabs-backward
             "C-<next>"  #'centaur-tabs-forward)
  :custom
  (centaur-tabs-enable-key-bindings t)
  (centaur-tabs-height 32)
  (centaur-tabs-set-icons t)
  (centaur-tabs-show-new-tab-button t)
  (centaur-tabs-set-modified-marker t)
  (centaur-tabs-show-navigation-buttons t)
  (centaur-tabs-set-bar 'right)
  (centaur-tabs-show-count nil)
  (centaur-tabs-left-edge-margin nil)
  (centaur-tabs-headline-match)
  :config (setq x-underline-at-descent-line t))
#+end_src

**** Emacs-Dashboard: A Home-Page for our Emacs

#+begin_src emacs-lisp
(use-package dashboard
  :after nerd-icons
  :hook (elpaca-after-init . dashboard-open)
  :custom-face
  (dashboard-heading ((t (:weight regular))))
  (dashboard-banner-logo-title ((t (:weight regular))))
  :custom
  (initial-buffer-choice (lambda ()
                           (dashboard-refresh-buffer)
                           (get-buffer dashboard-buffer-name)))

  (dashboard-display-icons-p t)
  (dashboard-icon-type 'nerd-icons)

  (dashboard-banner-logo-title "Welcome To The Underworld, Human. - Irkalla")
  (dashboard-center-content t)
  (dashboard-modify-heading-icons '((recents . "file-text")
                                    (bookmarks . "book")))
  (dashboard-startup-banner (expand-file-name "lib/logos/owl-skull.svg" user-emacs-directory))
  (dashboard-path-max-length 20)
  (dashboard-set-heading-icons t)
  (dashboard-set-file-icons t)
  (dashboard-set-init-info t)
  ;; (dashboard-projects-switch-function 'irkalla/switch-project-by-name) ;; :TODO| setup a projectile consult switcher
  (dashboard-week-agenda t)

  (dashboard-set-navigator t)
  (dashboard-navigator-buttons
   `(
     ((,(nerd-icons-octicon "nf-oct-mark_github" :height 1.2 :v-adjust 0.0)
       "Homepage"
       "Browse my personal GitHub profile home."
       (lambda (&rest _) (browse-url "https://github.com/Icy-Thought")))

      (,(nerd-icons-mdicon "nf-md-download" :height 1.2 :v-adjust 0.0)
       "Elpaca Manager"
       "Manage Irkalla Emacs packages."
       (lambda (&rest _) (elpaca-manager)))

      (,(nerd-icons-mdicon "nf-md-refresh" :height 1.2 :v-adjust 0.0)
       "Restart Emacs.."
       "Restart Irkalla Emacs instance."
       (lambda (&rest _) (restart-emacs))))))

  (dashboard-items '((recents   . 5)
                     (bookmarks . 5)
                     (projects  . 5)
                     (agenda    . 5)
                     (registers . 5)))

  (dashboard-item-names '(("Recent Files:" . "Recently opened files:")
                          ("Agenda for today:" . "Today's agenda:")
                          ("Agenda for the coming week:" . "Agenda:"))))
#+end_src

**** Telephone-Line: A Well-built Status-bar

#+begin_src emacs-lisp
(use-package telephone-line
  :hook ((prog-mode text-mode) . telephone-line-mode)
  :custom
  (telephone-line-height 26)
  (telephone-line-evil-use-short-tag t)

  ;; Left separator
  (telephone-line-primary-left-separator 'telephone-line-tan-left)
  (telephone-line-secondary-left-separator 'telephone-line-tan-hollow-left)

  ;; Right separator
  (telephone-line-primary-right-separator 'telephone-line-tan-right)
  (telephone-line-secondary-right-separator 'telephone-line-tan-hollow-right))
#+end_src

**** Nerd-icons: Icons to Decorate our Environment

#+begin_src emacs-lisp
(use-package nerd-icons
  :custom
  (nerd-icons-font-family (irkalla/default-font-family))
  (nerd-icons-scale-factors 1.25))
#+end_src

Allowing =Nerd-Icons= to display icons in =Vertico= is what the typical ricer deems worthy!

#+begin_src emacs-lisp
(use-package nerd-icons-completion
  :after (nerd-icons vertico)
  :hook (vertico-mode . nerd-icons-completion-mode))
#+end_src

**** SVG-Tags: A Ricer's Dream, SVG-based Tagging System!

***** Installing SVG-Tags

#+begin_src emacs-lisp
(use-package svg-tag-mode
  :hook ((prog-mode text-mode) . svg-tag-mode)
  :config
  (setq svg-lib-style-default
        (plist-put svg-lib-style-default :font-size 13.5))

  <<svg-tag-constants>>
  <<svg-tag-progress-bar>>
  <<svg-tags>>)
#+end_src

***** Defining Various SVG-Tags

****** Defining our Constants.

#+NAME: svg-tag-constants
#+begin_src emacs-lisp :tangle no
(defconst date-re "[0-9]\\{4\\}-[0-9]\\{2\\}-[0-9]\\{2\\}")
(defconst time-re "[0-9]\\{2\\}:[0-9]\\{2\\}")
(defconst day-re "[A-Za-z]\\{3\\}")
(defconst day-time-re (format "\\(%s\\)? ?\\(%s\\)?" day-re time-re))
#+end_src

****** Writing our SVG Progress-bar

#+NAME: svg-tag-progress-bar
#+begin_src emacs-lisp :tangle no
(defun svg-progress-percent (value)
    (svg-image (svg-lib-concat
                (svg-lib-progress-bar (/ (string-to-number value) 100.0)
                                      nil :margin 0 :stroke 2 :radius 3 :padding 2 :width 11)
                (svg-lib-tag (concat value "%")
                             nil :stroke 0 :margin 0)) :ascent 'center))

(defun svg-progress-count (value)
    (let* ((seq (mapcar #'string-to-number (split-string value "/")))
           (count (float (car seq)))
           (total (float (cadr seq))))
      (svg-image (svg-lib-concat
                  (svg-lib-progress-bar (/ count total) nil
                                        :margin 0 :stroke 2 :radius 3 :padding 2 :width 11)
                  (svg-lib-tag value nil
                               :stroke 0 :margin 0)) :ascent 'center)))
#+end_src

****** Ricing our SVG-Tags

#+NAME: svg-tags
#+begin_src emacs-lisp :tangle no
(setq svg-tag-tags
      `(
        ;; Org tags :THIS:
        ;; ("\\(:[A-Za-z0-9]+:\\)"
        ;;  . ((lambda (tag)
        ;;       (svg-tag-make tag :beg 1 :end -1 :inverse t))))

        ;; Task priority [#a]
        ("\\[#[a-zA-Z]\\]"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-priority
                            :beg 2 :end -1 :margin 0 :inverse t))))

        ;; Progress [1/3] or [45%]
        ("\\(\\[[0-9]\\{1,3\\}%\\]\\)"
         . ((lambda (tag)
              (svg-progress-percent (substring tag 1 -2)))))

        ("\\(\\[[0-9]+/[0-9]+\\]\\)"
         . ((lambda (tag)
              (svg-progress-count (substring tag 1 -1)))))

        ;; Specific tags -> allow spaces
        ;; :TODO| Reduce to a more general solution
        ;; :NOTE| Reduce to a more general solution
        ;; :FIXME| There is more regexp
        ;; :HACK| Fix this regexp
        ;; :WARN| This needs to be fixed

        ("\\([:]\\{1\\}\\W?\\(?:TODO\\|Todo\\)|.*\\)"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-code :inverse t :crop-left t :beg 6))))

        ("\\([:]\\{1\\}\\W?\\(?:TODO\\|Todo\\)*|\\)"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-code :inverse nil :margin 0 :crop-right t :beg 1 :end -1))))

        ("\\([:]\\{1\\}\\W?\\(?:WARN\\|Warn\\)|.*\\)"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-priority :inverse t :crop-left t :beg 7))))

        ("\\([:]\\{1\\}\\W?\\(?:FIXME\\|Fixme\\)|.*\\)"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-priority :inverse t :crop-left t :beg 7))))

        ("\\([:]\\{1\\}\\W?\\(?:HACK\\|PERF\\|MARK\\|Hack\\)|.*\\)"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-priority :inverse t :crop-left t :beg 6))))

        ("\\([:]\\{1\\}\\W?\\(?:HACK\\|Hack\\|PERF\\|WARN\\|Warn\\|FIXME\\|Fixme\\|MARK\\)*|\\)"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-priority :inverse nil :margin 0 :crop-right t :beg 1 :end -1))))

        ("\\([:]\\{1\\}\\W?\\(?:NOTE\\|Note\\)|.*\\)"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-cite :inverse t :crop-right t :beg 6))))

        ("\\([:]\\{1\\}\\W?\\(?:NOTE\\|Note\\)*|\\)"
         . ((lambda (tag)
              (svg-tag-make tag :face 'org-cite :inverse nil :margin 0 :crop-right t :beg 1 :end -1))))

        ;; Org TAGS
        (":TODO:" . ((lambda (tag) (svg-tag-make "TODO" :inverse t :face 'org-todo))))
        (":WIP:" . ((lambda (tag) (svg-tag-make "WIP" :inverse t :face 'org-cite))))
        (":DONE:" . ((lambda (tag) (svg-tag-make "DONE" :inverse t :face 'org-done))))
        (":NOTE:" . ((lambda (tag) (svg-tag-make "NOTE"))))
        ("SCHEDULED:" . ((lambda (tag) (svg-tag-make "SCHEDULED" :inverse t :face 'org-warning))))
        ("DEADLINE:" . ((lambda (tag) (svg-tag-make "DEADLINE" :inverse t :face 'org-priority))))
        ;; ("+BEGIN_SRC" . ((lambda (tag) (svg-tag-make "BEGIN" :inverse t :face 'org-code))))
        ;; ("+END_SRC" . ((lambda (tag) (svg-tag-make "END" :face 'org-code))))
        ;; ("+RESULTS:" . ((lambda (tag) (svg-tag-make "RESULTS" :face 'org-cite-key :underline nil))))
        (":X" . ((lambda (tag) (svg-tag-make "[X]" :inverse t :face 'org-checkbox-statistics-done))))
        (":-" . ((lambda (tag) (svg-tag-make "[-]" :inverse t :face 'org-checkbox))))

        ;; Citation of the form [cite:@Knuth:1984]
        ("\\(\\[cite:@[A-Za-z]+:\\)"
         . ((lambda (tag) (svg-tag-make tag :inverse t :beg 7 :end -1 :crop-right t))))

        ("\\[cite:@[A-Za-z]+:\\([0-9]+\\]\\)"
         . ((lambda (tag)
              (svg-tag-make tag :end -1 :crop-left t))))

          ;;; Works for stuff like :XXX|YYY:
        ("\\(:[A-Z]+\\)\|[a-zA-Z#0-9]+:"
         . ((lambda (tag)
              (svg-tag-make tag :beg 1 :inverse t :margin 0 :crop-right t))))

        (":[A-Z]+\\(\|[a-zA-Z#0-9]+:\\)"
         . ((lambda (tag)
              (svg-tag-make tag :beg 1 :end -1 :margin 0 :crop-left t))))

        ;; Active date (with or without day name, with or without time) <2023-04-03 Sun 17:45>
        (,(format "\\(<%s>\\)" date-re)
         . ((lambda (tag)
              (svg-tag-make tag :beg 1 :end -1 :margin 0))))

        (,(format "\\(<%s \\)%s>" date-re day-time-re)
         . ((lambda (tag)
              (svg-tag-make tag :beg 1 :inverse t :crop-right t :margin 0 :face 'org-agenda-date))))

        (,(format "<%s \\(%s>\\)" date-re day-time-re)
         . ((lambda (tag)
              (svg-tag-make tag :end -1 :inverse nil :crop-left t :margin 0 :face 'org-agenda-date))))))
#+end_src

*** Building a Versatile Toolbox

**** Default Application Launchers

#+begin_src emacs-lisp
(use-package openwith
  :hook (elpaca-after-init . openwith-mode)
  :config
  (setq openwith-associations
        (list (list (openwith-make-extension-regexp
                     '("mpg" "mpeg" "mp3" "mp4" "avi" "wmv" "wav"
                       "mov" "flv" "ogm" "ogg" "mkv"))
                    "mpv" '(file)))))
#+end_src

**** Consult: Consulting completing-read

#+begin_src emacs-lisp
(use-package consult
  :hook (completion-list-mode . consult-preview-at-point-mode)
  :general
  <<consult-bindings>>
  :config
  (setq register-preview-delay 0.5
        register-preview-function #'consult-register-format)
  (advice-add #'register-preview :override #'consult-register-window)
  ;; Consult -> select xref locations with preview
  (setq xref-show-xrefs-function #'consult-xref
        xref-show-definitions-function #'consult-xref)
  :custom
  (consult-narrow-key "<") ;; "C-+"

  (consult-customize
   consult-theme :preview-key '(:debounce 0.2 any)
   consult-ripgrep consult-git-grep consult-grep
   consult-bookmark consult-recent-file consult-xref
   consult--source-bookmark consult--source-file-register
   consult--source-recent-file consult--source-project-recent-file
   :preview-key '(:debounce 0.4 any)))
#+end_src

To reduce the burden of recalling all bindings, let's rewrite some of them to match our previous Neovim setup.

#+NAME: consult-bindings
#+begin_src emacs-lisp :tangle no
(irkalla/space-lead-keydef
  "/"          '(consult-ripgrep        :which-key "Quick RegExp grep")
  "b b"        '(consult-buffer         :which-key "Switch -> buffer")

  "p b"        '(consult-project-buffer :which-key "Switch -> project buffer")
  "p /"        '(consult-git-grep       :which-key "Grep .git repostiory")

  "f f"        '(consult-find           :which-key "File in Project")
  "f r"        '(consult-recent-file    :which-key "Recent files")

  ;; LSP-related
  "l"          '(:ignore t              :which-key "LSP & Editing")
  "l m"        '(consult-mark           :which-key "Jump -> marker")
  "l M"        '(consult-global-mark    :which-key "Glob. jump -> marker")
  "l o"        '(consult-outline        :which-key "Jump -> buffer outlines")
  "l ["        '(consult-flymake        :which-key "Jump -> Flymake diagnostics")
  "l ]"        '(consult-compile-error  :which-key "Jump -> compile-error in buffer"))

(irkalla/comma-lead-keydef
  "c"            '(:ignore t                   :which-key "Consult")
  "c b"          '(consult-bookmark            :which-key "Open named bookmark")
  "c h"          '(consult-history             :which-key "Insert string from hist.")
  "c k"          '(consult-kmacro              :which-key "Run KBD macro")
  "c ?"          '(consult-man                 :which-key "MAN-page str search")
  "c /"          '(consult-info                :which-key "MANUALS text search")
  "c p"          '(consult-yank-pop            :which-key "Paste yanks -> cursor")
  "c t"          '(consult-theme               :which-key "Select available themes")
  "c <return>"   '(consult-mode-command        :which-key "Run command")
  "c S-<return>" '(consult-complex-command     :which-key "Evaluate CMD from hist.")
  "c w"          '(consult-buffer-other-window :which-key "Frame buffer switch"))
#+end_src

Might as-well have consult integrate itself to our projectile and display existing files in current project directory.

#+begin_src emacs-lisp
(use-package consult-projectile
  :after projectile
  :general
  (irkalla/space-lead-keydef
    "p p" '(consult-projectile-switch-project :which-key "Switch Project")))
#+end_src

**** Dirvish: A polished Dired Front-end

#+begin_src emacs-lisp
(use-package dirvish
  :hook (dired-mode . dirvish-side-follow-mode)
  :general
  <<dirvish-bindings>>
  :config (dirvish-override-dired-mode)
  :custom
  (dirvish-side-width 30)
  (dirvish-use-header-line t)
  (dirvish-fd-default-dir "~/")
  (dirvish-quick-access-entries
   '(("h" "~/"                          "Home")
     ("d" "~/Downloads/"                "Downloads")
     ("m" "~/Library/unexplored"        "Library")
     ("t" "~/.local/share/Trash/files/" "Trash")))
  (dirvish-mode-line-format
   '(:left (sort symlink) :right (omit yank index)))
  (dirvish-attributes
   '(nerd-icons file-time file-size collapse subtree-state vc-state git-msg))
  (dired-listing-switches
   "-l --almost-all --human-readable --group-directories-first --no-group"))
#+end_src

#+NAME: dirvish-bindings
#+begin_src emacs-lisp :toggle no
(irkalla/comma-lead-keydef
  "f"   '(:ignore t    :which-key "Dirvish")
  "f f" '(dirvish-side :which-key "Dirvish Side-View")
  "f /" '(dirvish-fd   :which-key "Run fd in dir"))

(irkalla/comma-lead-keydef
  :keymaps 'dirvish-mode-map
  "a"   '(dirvish-quick-access        :which-key "Frequently used dirs")
  "f"   '(dirvish-file-info-menu      :which-key "File information")
  "y"   '(dirvish-yank-menu           :which-key "Yank marked files")
  "N"   '(dirvish-narrow              :which-key "Live narrowing")
  "^"   '(dirvish-history-last        :which-key "Goto recent buffer")
  "h"   '(dirvish-history-jump        :which-key "Goto recent dirs") ; remapped `describe-mode'
  "s"   '(dirvish-quicksort           :which-key "Sort buffers")     ; remapped `dired-sort-toggle-or-edit'
  "v"   '(dirvish-vc-menu             :which-key "Version control")  ; remapped `dired-view-file'
  "TAB" '(dirvish-subtree-toggle      :which-key "Dir -> sub-tree")
  "M-f" '(dirvish-history-go-forward  :which-key "History -> forward")
  "M-b" '(dirvish-history-go-backward :which-key "History -> backward")
  "M-l" '(dirvish-ls-switches-menu    :which-key "Setup listing switches")
  "M-m" '(dirvish-mark-menu           :which-key "Manage Marks")
  "M-t" '(dirvish-layout-toggle       :which-key "Toggle Fullscreen")
  "M-s" '(dirvish-setup-menu          :which-key "User Interface Setup")
  "M-e" '(dirvish-emerge-menu         :which-key "Manage Emerged Groups")
  "M-j" '(dirvish-fd-jump             :which-key "Setup fd-find Switches"))
#+end_src

More syntax-highlighting is never a bad idea, especially for navigating files/dirs!

#+begin_src emacs-lisp
(use-package diredfl
  :hook ((dired-mode dirvish-directory-view-mode) . diredfl-mode)
  :custom-face (diredfl-dir-name ((t :bold t))))
#+end_src

**** Eldoc: Document Things at Point

#+begin_src emacs-lisp
(use-package eldoc
  :elpaca nil
  :custom
  (eldoc-documentation-strategy #'eldoc-documentation-compose-eagerly)
  (eldoc-echo-area-display-truncation-message nil)
  (eldoc-echo-area-use-multiline-p nil)
  (eldoc-echo-area-prefer-doc-buffer t)
  (eldoc-idle-delay 1.0))
#+end_src

Having our Eldoc stuck in mini-buffer is not desirable, therefore it ought to be moved into its own posframe!

#+begin_src emacs-lisp
(use-package eldoc-box
  :after eldoc
  :general (general-nmap prog-mode-map
             "TAB" '(eldoc-box-help-at-point :which-key "LSP info at point")))
#+end_src

**** Embark: Mini-Buffer Actions Rooted in Keymaps

#+begin_src emacs-lisp
(use-package embark
  :hook (eldoc-documentation-functions . embark-eldoc-first-target)
  :general
  <<embark-bindings>>
  :custom
  (embark-prompter #'embark-completing-read-prompter)
  (embark-indicators '(embark-minimal-indicator
                       embark-highlight-indicator
                       embark-isearch-highlight-indicator))
  :config
  (setq prefix-help-command #'embark-prefix-help-command)
  ;; Hide the mode line of the Embark live/completions buffers
  (add-to-list 'display-buffer-alist
               '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                 nil
                 (window-parameters (mode-line-format . none)))))
#+end_src

Might as well add several bindings to our Embark.

#+NAME: embark-bindings
#+begin_src emacs-lisp :tangle no
(irkalla/comma-lead-keydef
  "e"   '(:ignore t       :which-key "Embark")
  "e a" '(embark-act      :which-key "Prompt for action -> perform")
  "e d" '(embark-dwim     :which-key "Run default action -> buffer")
  "e h" '(embark-bindings :which-key "Explore Emacs bindings"))
#+end_src

Integrating Embark with our consult package.

#+begin_src emacs-lisp
(use-package embark-consult
  :after (embark consult)
  :hook (embark-collect-mode . consult-preview-at-point-mode))
#+end_src

**** Version Control: Magit + Blamer + Git-Gutter

***** Magit: A Magic Wand for Git

#+begin_src emacs-lisp
(use-package magit
  :defer t
  :if (executable-find "git")
  :general
  <<magit-bindings>>
  :custom
  (magit-auto-revert-mode nil)
  (magit-display-buffer-function #'magit-display-buffer-fullframe-status-v1))
#+end_src

Appending bindings to our Magit configuration.

#+NAME: magit-bindings
#+begin_src emacs-lisp :tangle no
(irkalla/space-lead-keydef
  "g"   '(:ignore t :which-key "Magit")
  "g g" '(magit :which-key "Open Magit!"))
#+end_src

Display the ~TODO~'s of our Git repository in the Magit buffer. Useful for reducing the burden of grepping all the ~TODO~'s
and later deciding which one to hop-on to.

#+begin_src emacs-lisp
(use-package magit-todos
  :hook (magit-mode . magit-todos-mode)
  :custom
  (magit-todos-recursive t)
  (magit-todos-depth 10)
  (magit-todos-exclude-globs '(".git/" "*.html"))
  (magit-todos-nice (if (executable-find "nice") t nil))
  (magit-todos-scanner #'magit-todos--scan-with-rg)
  :config
  (custom-set-variables
   '(magit-todos-keywords (list "TODO" "FIXME" "HACK"))))
#+end_src

***** Blamer: Blame our Git Repository

#+begin_src emacs-lisp
(use-package blamer
  :defer t
  :custom
  (blamer-idle-time 0.5)
  (blamer-min-offset 70)
  (blamer-view 'overlay-right)
  (blamer-type 'visual)
  (blamer-max-commit-message-length 70)
  (blamer-force-truncate-long-line nil)
  (blamer-author-formatter " âœŽ %s ")
  (blamer-commit-formatter "â— \'%s\' â— ")
  :custom-face
  (blamer-face ((t (:background nil :height 125 :italic t)))))
#+end_src

***** Git-Gutter: Symbols to Highlight Git-related Changes

#+begin_src emacs-lisp
(use-package git-gutter
  :diminish git-gutter-mode
  :hook (prog-mode . git-gutter-mode)
  :custom (git-gutter:update-interval 0.05))
#+end_src

Replacing ~Git-Gutter~ symbols with appropriate vertical lines (fringes) appears to be a saner alternative to the default
symbols which comes with the package.

#+begin_src emacs-lisp
(use-package git-gutter-fringe
  :config
  (define-fringe-bitmap 'git-gutter-fr:added [224] nil nil '(center repeated))
  (define-fringe-bitmap 'git-gutter-fr:modified [224] nil nil '(center repeated))
  (define-fringe-bitmap 'git-gutter-fr:deleted [128 192 224 240] nil nil 'bottom))
#+end_src

**** Ement: A Superior Matrix Client

#+begin_src emacs-lisp
(use-package ement
  :defer t
  :hook (ement-room-mode . olivetti-mode)
  :custom
  (ement-room-images t)
  (ement-room-message-format-spec "%S> %W%B%r%R[%t]")
  (ement-room-send-message-filter #'ement-room-send-org-filter)

  ;; :NOTE| Notifications ought to be limited to @mentions only!
  (ement-notify-notification-predicates '(ement-notify--event-mentions-session-user-p
                                          ement-notify--event-mentions-room-p)))
#+end_src

A ~defun~ is required for us to launch a seperate scratchpad buffer for =Ement.el=!

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/ement-auto-connect ()
  "Connect Emacs to the Matrix, unless connection == existant."
  (interactive)
  (require 'ement)
  (unless ement-sessions
    (ement-connect
     :user-id "@gilganix:matrix.org"
     :password (irkalla/read-secret-file "ement")
     :uri-prefix "http://localhost:8009"))
  (ement-room-list))
#+end_src

**** PDF-Tools: A Quick PDF Viewer

#+begin_src emacs-lisp
(use-package pdf-tools
  :elpaca nil) ;; <-^ fetched from Nixpkgs
#+end_src

Make sure that our PDFâ€™s are displayed with darker colors.

#+begin_src emacs-lisp
(use-package pdf-view
  :elpaca nil
  :after pdf-tools
  :magic ("%PDF" . pdf-view-mode)
  :mode ("\\.[pP][dD][fF]\\'" . pdf-view-mode)
  :hook (pdf-view-mode . (lambda ()
                           ;; :NOTE| Set the PDF free from the unnecessary borders
                           (set (make-local-variable 'evil-normal-state-cursor) (list nil))
                           (pdf-view-themed-minor-mode)))
  :custom
  (pdf-view-use-scaling t)
  (pdf-view-use-imagemagick nil)
  (pdf-view-display-size 'fit-width))
#+end_src

We also want to jump back to the last known position after opening a PDF file.

#+begin_src emacs-lisp
(use-package pdf-view-restore
  :hook (pdf-view-mode . pdf-view-restore-mode)
  :custom (pdf-view-restore-filename (no-littering-expand-var-file-name "pdf-view-restore")))
#+end_src

**** NOV: A Customizable EPUB Reader

#+begin_src emacs-lisp
(use-package nov
  :mode ("\\.epub\\'" . nov-mode)
  :hook (nov-mode . (lambda ()
                      (olivetti-mode +1)
                      (visual-line-mode +1)
                      (visual-fill-column-mode +1)))
  :general
  <<nov-bindings>>
  :custom-face
  (shr-text ((t (:inherit variable-pitch-face :height 1.05))))
  (shr-h1   ((t (:height 1.54 :slant italic))))
  (shr-h2   ((t (:height 1.25 :slant italic))))
  (shr-h3   ((t (:height 1.15 :slant italic))))
  (shr-h4   ((t (:height 1.12 :slant italic))))
  (shr-h5   ((t (:height 1.09 :slant italic))))
  (shr-h6   ((t (:height 1.06 :slant italic))))

  :custom
  (nov-text-width t)
  (nov-text-width 120))
#+end_src

Defining bindings for our precious =Nov-Mode= to reduce the burden of navigation OTB.

#+NAME: nov-bindings
#+begin_src emacs-lisp :tangle no
(general-nmap nov-mode-map
  "H"   '(nov-previous-document   :which-key "Go -> previous doc")
  "L"   '(nov-previous-document   :which-key "Go -> next doc")
  "d"   '(nov-scroll-down         :which-key "Scroll downwards")
  "u"   '(nov-scroll-up           :which-key "Scroll upwards")
  "gm"  '(nov-display-metadata    :which-key "Show Metadata")
  "gr"  '(nov-render-document     :which-key "Render document")
  "o"   '(nov-goto-toc            :which-key "Table of contents")
  "gv"  '(nov-view-source         :which-key "View source")
  "gV"  '(nov-view-content-source :which-key "View content source"))
#+end_src

Allow ~NOV~ to display cleaner (+themed) EPUB files when launched.

#+begin_src emacs-lisp
(use-package nov-xwidget
  :elpaca (:host github :repo "chenyanming/nov-xwidget")
  :after nov
  :hook (nov-mode . nov-xwidget-inject-all-files)
  :general
  (general-nmap nov-mode-map
    "x" '(nov-xwidget-view :which-key "Open EPUB -> Nov-Mode")))
#+end_src

**** VTerm: A Quick Terminal Emulator

#+begin_src emacs-lisp
(use-package vterm
  :elpaca nil
  :hook (vterm-mode . (lambda () (setq-local global-hl-line-mode nil)))
  :general
  <<vterm-bindings>>
  :custom
  (vterm-timer-delay 0.01)
  (vterm-max-scrollback 10000)
  (vterm-clear-scrollback-when-clearing t))
#+end_src

Binding our VTerm for quicker access to the terminal.

#+NAME: vterm-bindings
#+begin_src emacs-lisp :tangle no
(general-imap vterm-mode-map
  "<S-prior>" #'scroll-down-command
  "<S-next>"  #'scroll-up-command)

(irkalla/space-lead-keydef
  "t"   '(:ignore t    :which-key "Terminal")
  "t t" '(vterm-toggle :which-key "Open VTerm"))

(irkalla/comma-lead-keydef
  "t"   '(:ignore t       :which-key "Terminal")
  "t c" '(vterm-copy-mode :which-key "Terminal -> read-only"))
#+end_src

#+begin_src emacs-lisp
(use-package vterm-toggle
  :after vterm
  :custom
  (vterm-toggle-fullscreen-p nil)
  (vterm-toggle-use-dedicated-buffer t))
#+end_src

**** Vertico: Vertical Interactive Completion

#+begin_src emacs-lisp
(use-package vertico
  :elpaca (:files (:defaults "extensions/*"))
  :hook (elpaca-after-init . vertico-mode)
  :custom
  (vertico-cycle t)
  <<vertico-multiform>>)
#+end_src

Controlling =Vertico='s behaviour in several forms has oftentimes proven to be rather useful!

#+NAME: vertico-multiform
#+begin_src emacs-lisp :tangle no
(vertico-multiform-categories
 '((file grid reverse)
   (consult-location buffer)
   (consult-grep buffer)
   (minor-mode reverse)
   (imenu buffer)
   (t unobtrusive)))

(vertico-multiform-commands
  '((consult-dir reverse)
    (execute-extended-command flat)
    (embark-prefix-help-command reverse)
    (completion-at-point reverse)))
#+end_src

Displaying our ~Vertico~ instances in their respective pop-up window would've been preferable.

#+begin_src emacs-lisp :tangle no
(use-package vertico-posframe
  :after vertico
  :hook (vertico-mode . vertico-posframe-mode)
  :custom
  (vertico-posframe-poshandler #'posframe-poshandler-frame-top-center)
  (vertico-posframe-fallback-mode vertico-buffer-mode))
#+end_src

Allowing our mouse to behave as expected when used in ~Vertico~.

#+begin_src emacs-lisp
(use-package vertico-mouse
  :elpaca nil
  :after vertico
  :hook (vertico-mode . vertico-mouse-mode))
#+end_src

Correcting navigation behaviour when interacting with directories in ~Vertico~.

#+begin_src emacs-lisp
(use-package vertico-directory
  :elpaca nil
  :after vertico
  :hook (rfn-eshadow-update-overlay . vertico-directory-tidy)
  :general (:keymaps 'vertico-map
             "RET"   #'vertico-directory-enter
             "DEL"   #'vertico-directory-delete-char
             "M-DEL" #'vertico-directory-delete-word))
#+end_src

Let's enhances the behavior & appearance of Emacs mini-buffer prompt:
1. Add a prompt indicator to the completing-read-multiple function
2. Make prompt read-only
3. Hides cursor within prompt
4. Prevents cursor from being placed inside prompt

#+begin_src emacs-lisp
(use-package emacs
  :elpaca nil
  :config
  (defun crm-indicator (args)
    (cons (format "[CRM%s] %s"
                  (replace-regexp-in-string "\\`\\[.*?]\\*\\|\\[.*?]\\*\\'" "" crm-separator)
                  (car args))
          (cdr args)))
  (advice-add #'completing-read-multiple :filter-args #'crm-indicator)

  (setq minibuffer-prompt-properties
	    '(read-only t cursor-intangible t face minibuffer-prompt))
  (add-hook 'minibuffer-setup-hook #'cursor-intangible-mode))
#+end_src

**** Marginalia: Mini-buffer Annotations

#+begin_src emacs-lisp
(use-package marginalia
  :hook (elpaca-after-init . marginalia-mode)
  :general (general-nmap minibuffer-local-map
             "M-A" '(marginalia-cycle :which-key "Cycle -> Marginalia annotators"))
  :custom
  (marginalia-max-relative-age 0)
  (marginalia-align 'right))
#+end_src

**** Screenshot: Useful for Spreading Emacs Love

#+begin_src emacs-lisp
(use-package screenshot
  :elpaca (:host github :repo "tecosaur/screenshot")
  :defer t)
#+end_src

*** Supercharging Irkalla's Mobility

**** EVIL: VIM bindings inside Emacs

#+begin_src emacs-lisp
(use-package evil
  :hook (elpaca-after-init . evil-mode)
  :general (general-nmap
             "M-j" #'pixel-scroll-up
             "M-k" #'pixel-scroll-down)
  :config (evil-set-undo-system 'undo-redo)
  :custom
  (evil-want-integration t)
  (evil-want-keybinding nil)
  (evil-split-window-below t)
  (evil-vsplit-window-right t))
#+end_src

Expanding the behaviour of =Evil-Mode= to support more scenarios/environments.

#+begin_src emacs-lisp
(use-package evil-collection
  :after evil
  :hook (evil-mode . evil-collection-init)
  :custom
  (evil-collection-magit-want-horizontal-movement t)
  (evil-collection-magit-use-y-for-yank t))
#+end_src

Surrounding items similar to VIM should

#+begin_src emacs-lisp
(use-package evil-surround
  :after evil
  :hook ((prog-mode text-mode) . evil-surround-mode))
#+end_src

Escaping in ~Evil-Mode~ should be relatively easy.

#+begin_src emacs-lisp
(use-package evil-escape
  :after evil
  :hook (evil-mode . evil-escape-mode)
  :delight (evil-escape-mode)
  :custom
  (evil-escape-key-sequence "jk")
  (evil-escape-delay 0.1)
  (evil-escape-unodered-key-sequence nil))
#+end_src

Displaying a visual hint for the actions performed in ~Evil-Mode~ seems to be reasonable and should therefore be included.

#+begin_src emacs-lisp
(use-package evil-goggles
  :after evil
  :hook (evil-mode . evil-goggles-mode)
  :custom
  (evil-goggles-enable-delete nil)
  (evil-goggles-duration 0.100)
  (evil-goggles-async-duration 0.900)
  (evil-goggles-use-diff-faces))
#+end_src

A quicker way to comment/uncomment certain blocks of text/code is desired.

#+begin_src emacs-lisp
(use-package evil-nerd-commenter
  :after evil
  :general
  (irkalla/space-lead-keydef
    ";" '(evilnc-comment-operator           :which-key "Un/Comment -> code-block"))
  (irkalla/comma-lead-keydef
    ";" '(evilnc-comment-or-uncomment-lines :which-key "Un/Comment -> line")))
#+end_src

**** Perspective: An Emacs Workspace Manager

#+begin_src emacs-lisp
(use-package perspective
  :hook (elpaca-after-init . persp-mode)
  :custom (persp-mode-prefix-key (kbd "C-c M-p")))
#+end_src

*** Tailoring our Editing Experience

#+begin_src emacs-lisp
(use-package emacs
  :elpaca nil
  :hook ((elpaca-after-init . pixel-scroll-precision-mode)
         (text-mode . auto-fill-mode)
         (text-mode . visual-line-mode))
  :custom
  (auto-save-interval 30)
  (confirm-nonexistent-file-or-buffer nil)
  (electric-indent-inhibit t)
  (find-file-suppress-same-file-warnings t)
  (indent-tabs-mode nil)
  (remote-file-name-inhibit-locks t)
  (standard-indent 4)
  (tab-width 4)
  (undo-limit 6710886400) ;; 64mb
  (undo-strong-limit 100663296) ;; x 1.5 (96mb)
  (undo-outer-limit 1006632960) ;; x 10 (960mb), (Emacs uses x100), but this seems too high.
  (word-wrap nil))
#+end_src

**** Citar: Quickly Find & Act on Bibliographic References

#+begin_src emacs-lisp
(use-package citar
  :hook ((LaTeX-mode org-mode) . citar-capf-setup)
  :custom (citar-bibliography '("~/Workspace/memorandum/references.bib")))
#+end_src

Integrating ~Citar~ with ~Embark~.

#+begin_src emacs-lisp
(use-package citar-embark
  :after (citar embark)
  :hook (org-mode . citar-embark-mode)
  :config (setq citar-at-point-function 'embark-act))
#+end_src

**** Ediff: UI for our Diff & Patches

#+begin_src emacs-lisp
(use-package ediff
  :elpaca nil
  :hook((ediff-prepare-buffer . outline-show-all)
        (ediff-quit . winner-undo))
  :custom
  (ediff-window-setup-function 'ediff-setup-windows-plain)
  (ediff-split-window-function 'split-window-horizontally)
  (ediff-merge-split-window-function 'split-window-horizontally))
#+end_src

**** EditorConf: Application of Pre-defined Project Modifications

#+begin_src emacs-lisp
(use-package editorconfig
  :hook (prog-mode . editorconfig-mode))
#+end_src

**** Electric Pair: Insert Matching SYMB Pairs

#+begin_src emacs-lisp
(use-package elec-pair
  :elpaca nil
  :hook ((prog-mode text-mode) . electric-pair-mode)
  :custom (electric-pair-inhibit-predicate 'electric-pair-conservative-inhibit))
#+end_src

**** Expand Region: Region-based Navigation

#+begin_src emacs-lisp
(use-package expand-region
  :general
  (general-vmap '(prog-mode-map text-mode-map)
    ")" 'er/expand-region
    "(" 'er/contract-region))
#+end_src

**** Highlight Indentation Guides

#+begin_src emacs-lisp
(use-package highlight-indent-guides
  :hook (prog-mode . highlight-indent-guides-mode)
  :custom
  (highlight-indent-guides-method 'column)
  (highlight-indent-guides-responsive 'top))
#+end_src

:TODO| colors -> indentation level + color change based on level of indentation

**** Interactive Align: Visualize Align-RegExp

#+begin_src emacs-lisp
(use-package ialign
  :general
  (irkalla/comma-lead-keydef 'visual (prog-mode-map text-mode-map)
    "a"   '(:ignore t :which-key "Alignment Control")
    "a r" '(ialign    :which-key "Align -> RegExp")))
#+end_src

**** Olivetti: Center Alignment of Content

#+begin_src emacs-lisp
(use-package olivetti
  :general
  (irkalla/comma-lead-keydef
    "q" '(olivetti-mode :which-key "Distraction free writing!"))
  :custom
  (olivetti-body-width 0.7)
  (olivetti-minimum-body-width 115)
  (olivetti-recall-visual-line-mode-entry-state t))
#+end_src

**** Rainbow-Mode: Colorize our Hex-Codes

#+begin_src emacs-lisp
(use-package rainbow-mode
  :hook (prog-mode . rainbow-mode))
#+end_src

**** Save-hist: Management of Our Cursed History

#+begin_src emacs-lisp
(use-package savehist
  :elpaca nil
  :hook ((prog-mode text-mode) . savehist-mode)
  :custom
  (history-length 1000)
  (history-delete-duplicates t)
  (savehist-autosave-interval 60)
  (savehist-save-minibuffer-history t)
  (savehist-file (no-littering-expand-var-file-name "savehist")))
#+end_src

**** Save-place: Recall Last Known Cursor Position

#+begin_src emacs-lisp
(use-package saveplace
  :elpaca nil
  :hook ((prog-mode text-mode) . savehist-mode)
  :custom
  (save-place-file (no-littering-expand-var-file-name "saveplace"))
  (save-place-forget-unreadable-files t))
#+end_src

**** Vundo: A Tree Viewer for Undo-Redo

#+begin_src emacs-lisp
(use-package vundo
  :general
  (irkalla/comma-lead-keydef
    "u" '(vundo :which-key "Visualize Undo"))
  :custom
  (vundo-compact-display t)
  (vundo-glyph-alist vundo-unicode-symbols))
#+end_src

**** Word-dependent Navigation

#+begin_src emacs-lisp
(use-package subword
  :elpaca nil
  :hook ((prog-mode text-mode) . subword-mode))
#+end_src

We also want to move to last known position in the buffer we just entered.
#+begin_src emacs-lisp
(use-package save-place
  :elpaca nil
  :hook ((prog-mode text-mode) . save-place-mode))
#+end_src

**** Whitespace: Customizing Look + Clean-up

#+begin_src emacs-lisp
(use-package whitespace
  :elpaca nil
  :defer t
  ;; :hook (prog-mode . whitespace-mode)
  :custom
  (whitespace-action '(cleanup auto-cleanup))
  (whitespace-style
   '(face spaces tabs newline trailing space-mark tab-mark newline-mark))
  (whitespace-display-mappings
   '(;; space -> Â· else .
     (space-mark 32 [183] [46])
     ;; new line -> Â¬ else $
     (newline-mark ?\n [172 ?\n] [36 ?\n])
     ;; carriage return (Windows) -> Â¶ else #
     (newline-mark ?\r [182] [35])
     ;; tabs -> Â» else >
     (tab-mark ?\t [187 ?\t] [62 ?\t]))))
#+end_src

*** Step-1: A More Efficient Completion System

**** Corfu: Completion in Region

#+begin_src emacs-lisp
(use-package corfu
  :elpaca (:files (:defaults "extensions/*.el"))
  :hook (elpaca-after-init . global-corfu-mode)
  :general (general-imap corfu-map
            "TAB"   #'corfu-next
            [tab]   #'corfu-next
            "S-TAB" #'corfu-previous
            [backtab] #'corfu-previous)
  :custom
  (corfu-auto t)
  (corfu-cycle t)
  (corfu-preselect t)
  (corfu-auto-prefix 1)
  (corfu-auto-delay 0.05)
  (corfu-count 16)
  (corfu-scroll-margin 5)
  (corfu-separator ?\s)
  (corfu-on-exact-match nil)
  (corfu-preview-current 'insert)
  (corfu-quit-no-match 'separator)
  :config
  (defun corfu-enable-always-in-minibuffer ()
    "Enable Corfu in the minibuffer if Vertico/Mct are not active."
    (unless (or (bound-and-true-p mct--active)
                (bound-and-true-p vertico--input)
                (eq (current-local-map) read-passwd-map))
      (setq-local corfu-echo-delay nil
                  corfu-popupinfo-delay nil)
      (corfu-mode 1)))
  (add-hook 'minibuffer-setup-hook #'corfu-enable-always-in-minibuffer 1))
#+end_src

Completion at point is necessary for Corfu to work its wonders as a completion system!

#+begin_src emacs-lisp
(use-package cape
  :config
  (dolist (fn '(cape-file cape-dabbrev cape-symbol cape-tex cape-keyword))
    (add-to-list 'completion-at-point-functions fn)))
#+end_src

Completions should display icons for their respective types.

#+begin_src emacs-lisp
(use-package kind-icon
  :after (svg-lib corfu)
  :custom
  (kind-icon-default-face 'corfu-default)
  ;; :NOTE| Changing the color of our icons
  (kind-icon-blend-background nil)
  (kind-icon-blend-frac 0.08)
  :config (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter))
#+end_src

~Corfu~ should also display its completions in a floating window.

#+begin_src emacs-lisp
(use-package corfu-popupinfo
  :elpaca nil
  :hook (corfu-mode . corfu-popupinfo-mode)
  :custom (corfu-popupinfo-delay '(0.5 . 0.2)))
#+end_src

~Corfu~ should also function inside our Emacs terminals.

#+begin_src emacs-lisp
(use-package corfu-terminal
  :unless window-system
  :hook (corfu-mode . corfu-terminal-mode))
#+end_src

**** Tempel: Simple Template (Snippets) System

#+begin_src emacs-lisp
;;;###autoload
(defun tempel-setup-capf ()
  "Integrating Temple with Corfu to automatically display possible snippet completions."
  (setq-local completion-at-point-functions
              (cons #'tempel-complete completion-at-point-functions)))
#+end_src

#+begin_src emacs-lisp
(use-package tempel
  :hook ((conf-mode prog-mode text-mode) . tempel-setup-capf)
  :config (setq-default tempel-path (expand-file-name "lib/snippets/*.eld" user-emacs-directory)))
#+end_src

Extending the support of ~Tempel~ should be one of our main priorities!

#+begin_src emacs-lisp
(use-package tempel-collection
  :after tempel)
#+end_src

:TODO|CDLaTeX Alternative

**** ChatGPT: Trained Machine Learning System (disabled)

#+begin_src emacs-lisp :tangle no
(use-package chatgpt-shell
  :general
  (irkalla/comma-lead-keydef
    "o c" '(chatgpt-shell :which-key "Open ChatGPT")
    "o d" '(dall-e-shell  :which-key "Open DALL-E"))
  :custom (chatgpt-shell-openai-key (lambda () (irkalla/read-secret-file "closedAI"))))
#+end_src

*** Step-2: LSP, Diagnostics & Syntax Highlighting!

#+begin_src emacs-lisp
(use-package emacs
  :elpaca nil
  :custom
  (completions-detailed t)
  (completion-ignore-case t)
  (compilation-scroll-output t)
  (tab-always-indent 'completion)
  (tab-first-completion 'word-or-paren-or-punct))
#+end_src

**** Apheleia: Asynchronous Code Formatter

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/apheleia-disable-formatting ()
  "When triggered -> disable apheleia formatting on save."
  (interactive)
  (with-eval-after-load 'apheleia
    (remove-hook 'before-save-hook 'eglot-format-buffer t)
    (apheleia-mode -1)))
#+end_src

Afterwards, /disable-on-demand/ formatting function should be bound to a memorable key combination.

#+begin_src emacs-lisp
(use-package apheleia
  :diminish apheleia-mode
  :hook (prog-mode . apheleia-mode)
  :general
  (irkalla/comma-lead-keydef
    "l"   '(:ignore t                           :which-key "LSP")
    "l d" '(irkalla/apheleia-disable-formatting :which-key "Disable Auto-format"))
  :config
  <<apheleia-custom-formatters>>)
 #+end_src

***** Self-defined Code Formatting Actions
:PROPERTIES:
:header-args+: :tangle no
:END:

#+NAME: apheleia-custom-formatters
#+begin_src emacs-lisp
<<apheleia-lang-haskell>>
<<apheleia-lang-nix>>
<<apheleia-lang-python>>
<<apheleia-lang-rust>>
#+end_src

****** Haskell: intelligent, but rather lazy..

#+NAME: apheleia-lang-haskell
#+begin_src emacs-lisp
(when (executable-find "stylish-haskell")
  (push '(stylish-haskell . ("stylish-haskell" "-"))
        apheleia-formatters)

  (setf (alist-get 'haskell-mode apheleia-mode-alist)
        '(stylish-haskell)))
#+end_src

****** Nix-Lang: stuck in a perfectionist's loop

#+NAME: apheleia-lang-nix
#+begin_src emacs-lisp
(when (executable-find "alejandra")
  (push '(alejandra . ("alejandra" "--quiet" "-"))
        apheleia-formatters)

  (setf (alist-get 'nix-mode apheleia-mode-alist)
        '(alejandra)))
#+end_src

****** Python: the odd ball who keeps growing

#+NAME: apheleia-lang-python
#+begin_src emacs-lisp
(when (executable-find "isort")
  (push '(isort . ("isort" "--profile" "black" "--stdout" "-"))
        apheleia-formatters)

  (setf (alist-get 'python-mode apheleia-mode-alist)
        '(isort black)))
#+end_src

****** Rust: wanna-be functional, but too literal

#+NAME: apheleia-lang-rust
#+begin_src emacs-lisp
(when (executable-find "rustfmt")
  (push '(rustfmt . ("rustfmt" "--quiet" "--emit" "stdout"))
        apheleia-formatters)

  (setf (alist-get 'rust-mode apheleia-mode-alist)
        '(rustfmt)))
#+end_src

**** Orderless: Completion That Matches Multiple Regexp

#+begin_src emacs-lisp
(use-package orderless
  :after vertico
  :custom
  (completion-styles '(orderless))
  (completion-category-defaults nil)
  (completion-category-overrides '((file (styles  . (orderless flex)))
                                   (eglot (styles . (orderless flex))))))
#+end_src

**** Eglot: Language Server Protocol

#+begin_src emacs-lisp
(use-package eglot
  :elpaca nil
  :general
  (irkalla/space-lead-keydef eglot-mode-map
    "l a"    '(eglot-code-actions    :which-key "Perform code-actions")
    "l r"    '(eglot-rename          :which-key "Rename $SYMB")
    "l f"    '(eglot-format          :which-key "Format buffer")
    "l ?"    '(xref-find-references  :which-key "Find -> references")
    "l d"    '(xref-find-definitions :which-key "Find -> definition")
    "l /"    '(xref-find-apropos     :which-key "Find $SYMB <- pattern"))
  :custom
  (eglot-autoshutdown t)
  (eglot-confirm-server-initiated-edits nil)
  (eglot-extend-to-xref t)
  (eglot-ignored-server-capabilities '(:documentHighlightProvider))
  (eglot-ignored-server-capabilities '(:hoverProvider))
  (eglot-menu-string "LSP"))
#+end_src

**** Sideline: Minimal UI for LSP Diagnostics

#+begin_src emacs-lisp
(use-package sideline
  :custom
  (sideline-delay 0.2)
  (sideline-display-backend-name nil)
  (sideline-display-backend-type 'inner))
#+end_src

**** Jinx: Fast Spell Checker

#+begin_src emacs-lisp
(use-package jinx
  :elpaca nil
  :hook (text-mode . global-jinx-mode)
  :general
  (general-nmap '(prog-mode-map text-mode-map)
    "z =" '(jinx-correct :which-key "Correct damned misspellings...")))
#+end_src

**** Flymake: Built-in Syntax Checker

#+begin_src emacs-lisp
(use-package flymake
  :elpaca nil
  :hook (prog-mode . flymake-mode)
  :custom
  ;; (flymake-show-diagnostics-at-end-of-line t) <- way to much info..
  (flymake-fringe-indicator-position 'right-fringe)
  (elisp-flymake-byte-compile-load-path load-path))
#+end_src

Extending the diagnostic ability of ~Flymake~ to support an additional set of language tools.

#+begin_src emacs-lisp
(use-package flymake-collection
  :after flymake
  :hook (flymake-mode . flymake-collection-hook-setup))

;; :NOTE| Appending :flymake-hook to the keywords of use-package!
(elpaca-wait)
#+end_src

Preventing ~Eglot~ from interfering with our ~Flymake~ diagnostics is preferable.

#+begin_src emacs-lisp
(with-eval-after-load "eglot"
 (add-to-list 'eglot-stay-out-of 'flymake)
 (add-hook 'flymake-diagnostic-functions 'eglot-flymake-backend))
#+end_src

***** Sideline: Display Flymake Diagnostics on Hover

#+begin_src emacs-lisp
(use-package sideline-flymake
  :hook (flymake-mode  . sideline-mode)
  :custom (sideline-backends-right '((sideline-flymake  . down))))
#+end_src

**** Tree-Sitter: Quicker Syntax Highlighting + More!

#+begin_src emacs-lisp
(use-package tree-sitter
  :hook ((prog-mode . global-tree-sitter-mode)
         (tree-sitter-after-on . tree-sitter-hl-mode)))
#+end_src

***** TS-fold: Folding Code-blocks Like Origami

#+begin_src emacs-lisp
(use-package ts-fold
  :elpaca (ts-fold :host github :repo "emacs-tree-sitter/ts-fold")
  :hook (prog-mode . global-ts-fold-mode))
#+end_src

*** Step-3: Language-specific Adjustments

**** Parinfer: Controlling Wild Lisp Brackets

#+begin_src emacs-lisp
(use-package parinfer-rust-mode
  :hook (emacs-lisp-mode . (lambda ()
                             (electric-pair-mode -1)
                             (parinfer-rust-mode)))
  :custom
  (parinfer-rust-auto-download t)
  (parinfer-rust-library-directory (no-littering-expand-var-file-name "parinfer-rust/")))
#+end_src

**** Haskell: Lazy & Statically Typed FP Language

#+begin_src emacs-lisp
(use-package haskell-mode
  :mode ("\\.hs\\'" . haskell-mode)
  :hook (haskell-mode . eglot-ensure)
  :config
  (setq-default eglot-workspace-configuration
                '((haskell (formattingProvider "stylish-haskell")))))
#+end_src

**** Markdown: Simple Markup Language

#+begin_src emacs-lisp
(use-package markdown-mode
  :mode ("\\.md\\'" . gfm-mode)
  :general
  (irkalla/comma-lead-keydef markdown-mode-map
    "m d"    '(markdown-do :which-key "Perform -> action"))
  :custom-face
  (markdown-header-face-1 ((t (:inherit markdown-header-face :height 1.25 :weight extra-bold))))
  (markdown-header-face-2 ((t (:inherit markdown-header-face :height 1.15 :weight bold))))
  (markdown-header-face-3 ((t (:inherit markdown-header-face :height 1.08 :weight bold))))
  (markdown-header-face-4 ((t (:inherit markdown-header-face :height 1.00 :weight bold))))
  (markdown-header-face-5 ((t (:inherit markdown-header-face :height 0.90 :weight bold))))
  (markdown-header-face-6 ((t (:inherit markdown-header-face :height 0.75 :weight extra-bold))))
  :custom (markdown-command "multimarkdown"))
#+end_src

**** Nix: Dynamic Typed FP Language Used in NixOS

#+begin_src emacs-lisp
(use-package nix-mode
  :mode ("\\.nix\\'" . nix-mode)
  :hook (nix-mode . eglot-ensure)
  :config
  (with-eval-after-load 'eglot
   (add-to-list 'eglot-server-programs '(nix-mode . ("nil")))))
#+end_src

**** Org-Mode: The Superior File-format

Extending =Electric-Pairs= ability to insert pairs in ~Org-Mode~:
1. Insert LaTeX brackets on ~$~ insertion

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/org-electric-dollar nil
  "Inserts \\( \\) when $, and replaces it with \\[ \\] when $$."
  (interactive)
  (if (and (looking-at "\\\\)")
           (looking-back "\\\\("))
      (progn (delete-char 2)
             (delete-char -2)
             (insert "\\[\\]"))
    (insert "\\(\\)")
    (backward-char 2)))
#+end_src

Later, we proceed with the customization of our ~Org-Mode~ environment.

#+begin_src emacs-lisp
(use-package org
  :elpaca nil
  :defer t
  :hook (org-mode . org-display-inline-images)
  :general
  (irkalla/comma-lead-keydef
    "o"   '(:ignore t        :which-key "Org-Mode")
    "o e" '(org-edit-special :which-key "Edit -> special buffer")
    "o t" '(org-babel-tangle :which-key "Tangle buffer"))

  (general-imap org-mode-map
    "$" #'irkalla/org-electric-dollar)
  :custom-face
  (org-document-title ((t (:height 1.50))))
  (org-level-1        ((t (:inherit outline-1 :height 1.25))))
  (org-level-2        ((t (:inherit outline-2 :height 1.15))))
  (org-level-3        ((t (:inherit outline-3 :height 1.12))))
  (org-level-4        ((t (:inherit outline-4 :height 1.09))))
  (org-level-5        ((t (:inherit outline-5 :height 1.06))))
  :config
  (setq-default org-directory "~/Workspace/memorandum/org-mode")
  
  ;; :NOTE| Move our LaTeX previews to cache dir
  (let ((latex-dir (no-littering-expand-var-file-name "latex-preview/")))
    (unless (file-directory-p latex-dir)
      (mkdir latex-dir t))
    (setq-default org-preview-latex-image-directory latex-dir))

  ;; :NOTE| Change the aesthetics of our LaTeX previews
  (setq-default org-latex-preview-options
                (progn (plist-put org-format-latex-options :background "Transparent")
                       (plist-put org-format-latex-options :scale 2.5)
                       (plist-put org-format-latex-options :zoom 1.15)))
  :custom
  (org-fontify-quote-and-verse-blocks t)
  (org-catch-invisible-edits 'show-and-error)
  (org-cycle-separator-lines 2)
  (org-cycle-include-plain-lists 'integrate)
  (org-edit-src-auto-save-idle-delay 5)
  (org-ellipsis "â€¦")
  (org-export-coding-system 'utf-8)
  (org-export-preserve-breaks t)
  (org-hide-emphasis-markers t)
  (org-highlight-latex-and-related '(native))
  (org-image-actual-width (truncate (* (window-pixel-width) 0.8)))
  (org-insert-heading-respect-content t)
  (org-latex-tables-centered t)
  (org-special-ctrl-a/e t)
  (org-startup-folded 'overview)
  (org-startup-indented t)
  (org-startup-with-inline-images t)
  (org-support-shift-select t)
  (org-tags-column 0)

  ;; Code blocks
  (org-confirm-babel-evaluate nil)
  (org-edit-src-content-indentation 0)
  (org-src-fontify-natively t)
  (org-src-preserve-indentation t)
  (org-src-tab-acts-natively nil))
#+end_src

Automatically render our LaTeX code-blocks.

#+begin_src emacs-lisp
(use-package org-fragtog
  :after org
  :hook (org-mode . org-fragtog-mode))
#+end_src

We also want to generate our "Table of Contents" on the fly.

#+begin_src emacs-lisp
(use-package toc-org
  :after org
  :hook (org-mode . toc-org-enable)
  :custom (toc-org-max-depth 3))
#+end_src

***** Org-Modern: A Modern Org-Mode Look

#+begin_src emacs-lisp
(use-package org-modern
  :after org
  :hook (org-mode . org-modern-mode)
  :custom-face
  (org-modern-symbol ((t (:family "DejaVu Sans"))))
  :custom
  ;; :NOTE| Settings replaced by svg-tag-mode
  (org-modern-tag nil)
  (org-modern-todo nil))
#+end_src

***** Org-Roam: A Powerful Note-Taking System

#+begin_src emacs-lisp
(use-package org-roam
  :after org
  :general
  (irkalla/comma-lead-keydef org-mode-map
    "o r"   '(:ignore t               :which-key "Org-Roam")
    "o r l" '(org-roam-buffer-toggle  :which-key "Org-Roam -> buffer")
    "o r n" '(org-roam-node-find      :which-key "Open node -> title/alias")
    "o r g" '(org-roam-graph          :which-key "Build -> show node of graph")
    "o r i" '(org-roam-node-insert    :which-key "Find node -> insert `:id` org-link")
    "o r c" '(org-roam-capture        :which-key "Open org-capture of node"))
  :custom
  (org-roam-directory (file-truename "~/org/org-roam"))
  (org-roam-completion-everywhere t)
  (org-roam-capture-templates
   `(("d" "default" plain "%?"
      :if-new (file+head
               "%<%Y%m%d%H%M%S>-${slug}.org"
               ,(let ((options '("#+options: _:{}"
                                 "#+options: ^:{}"
                                 "#+startup: latexpreview"
                                 "#+startup: entitiespretty"
                                 "#+startup: inlineimages"
                                 "#+title: ${title}")))
                  (mapconcat 'identity options "\n")))
      :unnarrowed t)))
  (org-roam-node-display-template "${title}"))
#+end_src

A GUI is desired for our Org-Roam to reduce the burden of browsing our notes.

#+begin_src emacs-lisp
(use-package org-roam-ui
  :after org-roam
  :custom
  (org-roam-ui-sync-theme t)
  (org-roam-ui-follow t)
  (org-roam-ui-update-on-save t)
  (org-roam-ui-open-on-start nil))
#+end_src

***** Org-Babel: Execution of Org SRC-blocks

#+begin_src emacs-lisp
;;;###autoload
(defun irkalla/org-execute-action ()
  "Execute the appropriate action based on the given context."
  (interactive)
  (let ((context (org-element-context)))
    (pcase (org-element-type context)
      (`src-block
       ;; In a source block, call `org-babel-execute-src-block'.
       (org-babel-eval-wipe-error-buffer)
       (org-babel-execute-src-block current-prefix-arg))
      (`babel-call
       ;; In a `:+CALL:' block, call `org-babel-execute-maybe'.
       (call-interactively #'org-babel-execute-maybe))
      (`table-row
       ;; In a table or table-cell, call `org-table-next-row'.
       (call-interactively #'org-table-next-row))
      ((or `link `timestamp)
       ;; On a link or a timestamp, call `org-open-at-point'.
       (call-interactively #'org-open-at-point))
      (_
       ;; Fallback to evil standard command
       (call-interactively #'evil-ret)))))
#+end_src

Modifying ~Org-Tangle~ to suit our tedious workflow is our main priority here.

#+begin_src emacs-lisp
(use-package ob
  :elpaca nil
  :after org
  :hook (org-babel-after-execute . org-display-inline-images)
  :general (general-nmap org-mode-map
             "RET" '(irkalla/org-execute-action :which-key "Execute appropriate Org action"))
  :custom
  (org-babel-default-header-args
   '((:async   . "yes")
     (:cache   . "no")
     (:eval    . "never-export")
     (:exports . "code")
     (:hlines  . "no")
     (:noweb   . "yes")
     (:results . "replace")
     (:session . "none")
     (:tangle  . "no")))
  (org-export-use-babel nil)
  (org-confirm-babel-evaluate nil)
  :config
  (defadvice org-babel-execute-src-block (around load-language nil activate)
   "Load Org-babel languages on demand."
   (let ((language (org-element-property :language (org-element-at-point))))
     (unless (cdr (assoc (intern language) org-babel-load-languages))
       (add-to-list 'org-babel-load-languages (cons (intern language) t))
       (org-babel-do-load-languages 'org-babel-load-languages org-babel-load-languages))
     ad-do-it)))
#+end_src

**** Python: Boring High-level GP Language

#+begin_src emacs-lisp
(use-package python-mode
  :mode ("\\.py\\'" . python-mode)
  :hook (python-mode . eglot-ensure)
  :flymake-hook
  (python-mode flymake-collection-mypy
               flymake-collection-ruff))
#+end_src

**** Rust: A Type-safe GP Language

#+begin_src emacs-lisp
(use-package rust-mode
  :mode ("\\.rs$" . rust-mode)
  :hook (rust-mode . (lambda ()
                       (eglot-ensure)
                       (setq indent-tabs-mode nil)))
  :general
  (irkalla/comma-lead-keydef rust-mode-map
    "r"   '(:ignore t       :which-key "Rust")
    "r b" '(rust-compile    :which-key "Compile project")
    "r c" '(rust-check      :which-key "Compile + cargo check")
    "r l" '(rust-run-clippy :which-key "Run cargo clippy")
    "r r" '(rust-run        :which-key "Run project")
    "r t" '(rust-test       :which-key "Run tests on project"))
  :config
  (add-to-list 'eglot-server-programs
               `(rust-mode . (:rust-analyzer
                              ;; https://rust-analyzer.github.io/manual.html
                              (:initializationOptions
                               :cargo       (:features "all")
                               :completion  (;; Completion-related
                                             :callable (:snippets "fill_arguments"))
                               :checkOnSave (;; Actions to check upon saving
                                             :command "clippy"
                                             :allTargets :json-false)))))

  (defun irkalla/locate-cargo-toml (dir)
    "Locate the missing Rust project Cargo.toml."
    (if-let ((root (locate-dominating-file dir "Cargo.toml")))
        (list 'vc 'Git root)))
  (add-hook 'rust-mode-hook
            (lambda () (add-to-list 'project-find-functions #'irkalla/locate-cargo-toml))))
#+end_src

Integrating =Cargo= with our newly added =Rust-Mode= would help us make our environment more lively!

#+begin_src emacs-lisp
(use-package cargo
  :hook (rust-mode . cargo-minor-mode)
  :general
  (irkalla/comma-lead-keydef rust-mode-map
    "r p a" '(cargo-process-add   :which-key "Cargo Add")
    "r p c" '(cargo-process-clean :which-key "Cargo Clean"))
  :custom (cargo-process--command-clippy "clippy"))
#+end_src

Being able to retain our notes in ~Org-Mode~ would've been wonderful. Therefore we ought to make it possible!

#+begin_src emacs-lisp
  (use-package ob-rust
    :after ob)
#+end_src

**** Typst: The Modern & Cleaner LaTeX

#+begin_src emacs-lisp
(use-package typst-mode
  :elpaca (:host github :repo "Ziqi-Yang/typst-mode.el")
  :mode ("\\.typ\\'" . typst-mode))
#+end_src

**** Yuck: Elkowar's Lisp Dialect for EWW (disabled)

#+begin_src emacs-lisp :tangle no
(use-package yuck-mode)
#+end_src

** Concluding Our Journey with Irkalla Emacs

#+begin_src emacs-lisp
;;; init.el ends here
#+end_src
